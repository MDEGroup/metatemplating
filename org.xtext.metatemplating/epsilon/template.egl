[%import "metaMetaEngine.eol";%]
[%if (s.header <> null) {
	for (lib in s.header.libs) { %]
[%=insStat("import \"" + lib.path + "\";")%]
	[%}
	for (imp in s.header.imports) { %]
[%=insMetaStat(insStr("import \\\"") + " + " + insStr(imp.path) + " + " + insStr("\\\";"))%]
	[%}
}%]
[%=insComment("Meta-template generated for " + fileName)%]
[%=s.parseContent()%]

[*-Content*]
[%operation Any parseContent(): String {
	var result = "";
	for (istr in self.content) {
		result += istr.parseInstructions(0);
	}
	return result;
}%]

[*-Instructions*]
[%operation Any parseInstructions(id: Integer): String {
	var result = "";
	if (self.note <> null) {
		result += insComment(strFromList(self.note.word)) + "\n";
	}
	if (self.statement <> null) {
		result += self.statement.parseStatement();
	}
	if (self.iterator <> null) {
		result += insLine(id, insMetaFor("'" + self.iterator.element + "'", self.iterator.query.parseQuery()));
		for (istr in self.iterator.body) {
			result += istr.parseInstructions(id + 1);
		}
		result += insLine(id, insMetaEnd());
	}
	if (self.rule <> null) {
		result += insLine(id, insFor(self.rule.element, self.rule.property.parseProperty() + self.rule.tag.parseTagged()));
		for (istr in self.rule.body) {
			result += istr.parseInstructions(id + 1);
		}
		result += insLine(id, insEnd());
	}
	return result;
}%]

[*-Property*]
[%operation Any parseProperty(): String {
	var result = self.property;
	for (subp in self.subProperties) { 
		result += subp.parseSubProperty();
	}
	return result;
}%]

[*-SubProperty*]
[%operation Any parseSubProperty(): String {
	if (self.property <> null) return '.' + self.property;
	else if (self.method <> null) return '.' + self.method + '()';
}%]

[*-Tag*]
[%operation Any parseTagged(): String {
	return '.getWanted(_source, "' + self + '")';
}%]

[*-Query*]
[%operation Any parseQuery(): String {
	var result = "";
	if (self.item <> null) result += "'" + self.item + "'";
	if (self.ref <> null) result += self.ref.parseMetaPh();
	for (subq in self.subQuery) {
		result += subq.parseSubQuery();
	}
	return result;
}%]

[*-SubQuery*]
[%operation Any parseSubQuery(): String {
	var result = "";
	if (self.item <> null) result += " + '." + self.item + "'";
	if (self.ref <> null) result += " + '.' + " + self.ref.parseMetaPh();
	if (self.methItem <> null) result += " + '." + self.methItem + "()'";
	if (self.methRef <> null) result += " + '.' + " + self.parseMetaPh() + '()';
	return result;
}%]

[*-MetaPh*]
[%operation Any parseMetaPh(): String {
	var result = self.property.parseProperty();
	if (self.tag <> null) result += '.getExtraInfo(' + self.tag + ')';
	return result;
}%]

[*-Statement*]
[%operation Any parseStatement(): String {
	if (self.text <> null) return self.text;
	if (self.char <> null) return self.char.parseEscaped();
	if (self.string <> null) return self.string.str;
	if (self.placeholder <> null) return insMetaPh(self.placeholder.parsePh());
	if (self.metaPlaceholder <> null) return insPh(self.metaPlaceholder.parseMetaPh());
}%]

[*-Escaped*]
[%operation Any parseEscaped(): String {
	if (self.char == 'n') return "\n";
	else if (self.char == 's') return " ";
	else if (self.char == 't') return "\t";
	else return self.char;
}%]

[*-Ph*]
[%operation Any parsePh(): String {
	var result = "";
	if (self.metaProperty == null) {
		result += "'" + self.property.parseProperty() + "'";
	}
	else {
		result += "'" + self.property.parseProperty() + ".' + "
			+ self.metaProperty.parseMetaProperty();
	} 
	return result;
}%]

[*-MetaProperty*]
[%operation Any parseMetaProperty(): String {
	return self.property.parseProperty();
}%]