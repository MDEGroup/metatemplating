grammar org.xtext.example.mydsl.MetaTemplating with org.eclipse.xtext.common.Terminals

generate metaTemplating "http://www.xtext.org/example/mydsl/MetaTemplating"

Model:
	header=Header
	content+=Instructions*
;

/* Header */

Header:
	{Header} libs+=Libraries*
	imports+=Import*
;

Import:
	'import' path=STRING ';'
;

Libraries:
	'IMPORT' path=STRING ';'
;

/* Notes */

Note:
	'#*' word+=ID+ '*#'
;

/* Instructions */

Instructions:	
	note=Note |
	statement=Statement |
	filter=Filter |
	metaFilter=MetaFilter |
	iterator=Iterator | 
	rule=Rule
;

Statement:
	text=ID | char=Escaped | string=EscapedString |
	inject=Inject | metaInject=MetaInject |
	placeholder=Ph | metaPlaceholder=MetaPh
;

/* Filters */

MetaFilter:
	'IF' '(' condition=Property ')' '{' truebody+=Instructions* '}' ('ELSE' '{' falsebody+=Instructions* '}' )?
;

Filter:
	'if' '(' condition=Query ')' '{' truebody+=Instructions* '}' ('else' '{' falsebody+=Instructions* '}' )?
;

/* Rules */
	
Rule:
	'FORALL' '(' element=ID ':' property=Property 'TAGGED' tag=ID ')' '{' body+=Instructions* '}'
;

Property:
	property=ID subProperties+=SubProperty*
;

SubProperty:	
	'.' method=ID '(' params=Parameter? ')' | 
	'.' property=ID 
; 

/* Iterators */
	
Iterator:
	'foreach' '(' element=ID 'in' query=Query ')'
	'{' body+=Instructions* '}'
;	

Query:
	(item=ID | ref=MetaPh) subQuery+=SubQuery*
;

SubQuery:
	'.' (methItem=ID | methRef=MetaPh) '(' params=Parameter? ')' |
	'.' (item=ID | ref=MetaPh)
;

/* Placeholders */

Ph:
	'[[' property=PhProperty ']]'
;

MetaPh:
	'[' property=Property ('TAGINFO' tag=ID)? ']'
;

PhProperty:
	property=ID subProperties+=PhSubProperty*
;

PhSubProperty:	
	'.' method=ID '(' params=Parameter? ')' | 
	'.' property=ID |
	'.{' metaProperty=Property '}'
; 


/* Parameters */

Parameter:
	par=ID params+=SubParameter*
;

SubParameter:
	',' par=ID
;

/* Inject */
Inject:
	'@[['code=STRING']]@'
;

MetaInject:
	'@['code=STRING']@'
;

/* Escaping */

EscapedString:
	'@{' str=STRING '}@'
;

Escaped:
	'#' char=('n' | 's' | 't' | '#' | '{' | '}' | '[' | ']' | ',' | '.' | ';' | '(' | ')' | ':' | '<' | '>' | "'")
;
