grammar org.xtext.example.mydsl.MetaTemplating with org.eclipse.xtext.common.Terminals

generate metaTemplating "http://www.xtext.org/example/mydsl/MetaTemplating"

Model:
	header=Header
	content+=Instructions*
;

/* Header */

Header:
	{Header} libs+=Libraries*
	imports+=Import*
;

Import:
	'import' path=STRING ';'
;

Libraries:
	'IMPORT' path=STRING ';'
;

/* Comments */

Comment:
	'//' word+=ID |
	'/*' word+=ID '*/'
;

/* Instructions */

Instructions:	
	statement=Statement |
	iterator=Iterator | 
	rule=Rule |
	comment=Comment
;

Statement:
	text=ID | char=Escaped | string=EscapedString |
	placeholder=Ph | metaPlaceholder=MetaPh
;

/* Rules */
	
Rule:
	'FORALL' '(' element=ID ':' property=Property 'TAGGED' tag=ID ')' '{' body+=Instructions* '}'
;

/* Iterators */
	
Iterator:
	'foreach' '(' element=ID 'in' query=Query ')'
	'{' body+=Instructions* '}'
;	

Query:
	(item=ID | ref=MetaPh) subQuery+=SubQuery*
;

SubQuery:
	'.' (item=ID | ref=MetaPh) '()'?
;

/* Placeholder */

Ph:
	'[[' property=Property (metaProperty=MetaProperty)? ']]'
;

MetaPh:
	'[' property=Property ('TAGINFO' tag=STRING)? ']'
;

MetaProperty:
	'.{' property=Property '}'	
;

/* Properties */

Property:
	property=ID subProperties+=SubProperty*
;

SubProperty:	
	'.' property=ID '()'?
; 

/* Escaping */

EscapedString:
	'@{' str=STRING '}@'
;

Escaped:
	'#' char=('#' | '{' | '}' | '[' | ']' | ',' | ';' | '(' | ')' | ':' | '<' | '>' | "'")
;
